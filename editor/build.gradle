

plugins {
    id 'java-library'
    id "io.freefair.lombok" version "6.4.1"
}

dependencies {

    api "com.rockbite.bongo:engine"
    api "com.rockbite.bongo:talos"

    implementation "com.kotcrab.vis:vis-ui:$visuiVersion"

    implementation "info.debatty:java-string-similarity:1.2.1"
    implementation "com.esotericsoftware.spine:spine-libgdx:4.0.18.1"

    implementation "org.yaml:snakeyaml:1.21"

    implementation "com.asidik:jtinygizmo:0.0.1-SNAPSHOT"
    implementation "com.asidik:jtinygizmo:0.0.1-SNAPSHOT:natives-desktop"

}

sourceCompatibility = "1.8"

sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs = ["assets"]


task createProperties(dependsOn: processResources) {
    doLast {
        new File("$projectDir/assets/talos-version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

task packageSources(type: Jar, dependsOn: classes) {
//    from sourceSets.main.allJava.srcDirs
    from delombok
    from sourceSets.main.resources.srcDirs
    archiveClassifier = 'web'
}

configurations {
    web {
        canBeConsumed true
        canBeResolved false
        extendsFrom implementation, runtimeOnly
    }
}


artifacts {
    web(packageSources)
}

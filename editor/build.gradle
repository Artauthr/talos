buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' } // snapshots
    }
    dependencies {
        classpath 'com.squareup:javapoet:1.13.0'
        classpath "com.badlogicgames.gdx:gdx:1.10.1-SNAPSHOT"
    }
}

plugins {
    id 'java-library'
    alias(libs.plugins.lombok)
}


import com.badlogic.gdx.files.FileHandle
import com.badlogic.gdx.utils.XmlReader
import com.squareup.javapoet.ClassName
import com.squareup.javapoet.FieldSpec
import com.squareup.javapoet.JavaFile
import com.squareup.javapoet.MethodSpec
import com.squareup.javapoet.TypeSpec

import javax.lang.model.element.Modifier

dependencies {

    api libs.bongo.engine
    api project(":runtimes:talos-bongo")

    api libs.visui

    implementation libs.string.similarity

    implementation libs.spine.libgdx

    implementation libs.snakeyaml

    implementation libs.tiny.gizmo
    implementation libs.spark
    implementation libs.javapoet

}


sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs = ["assets"]




def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

task createProperties () {
    doFirst {

        def file = rootProject.file("editor/assets/talos-version.properties");
        Properties properties = new Properties();
        if (file.exists()) {
            properties.load(file.newReader());
        }
        properties.setProperty("version", project.version);
        properties.setProperty("buildHash", getGitHash());
        properties.setProperty("buildTime", System.currentTimeMillis() + "");

        properties.store(file.newPrintWriter(), "");
    }
}

task generateActionsEnum () {
    doFirst {
        TypeSpec.Builder builder = TypeSpec.classBuilder("Actions").addModifiers(Modifier.PUBLIC)
                .addJavadoc("This is a generated class. It shouldn't be modified by hand, as the changes would be " +
                "overridden.\n")
                .addJavadoc("To regenerate this class, call generateActionsEnum task from Gradle.\n " +
                        "The XML file is located in editor/assets/actions.xml")
        def file = rootProject.file("editor/assets/actions.xml")
        XmlReader xmlReader = new XmlReader()
        XmlReader.Element root = xmlReader.parse(new FileHandle(file))

        TypeSpec.Builder interfaceBuilder = TypeSpec.interfaceBuilder("ActionEnumInterface").addModifiers(Modifier.PUBLIC)
        TypeSpec interfaceBuilt = interfaceBuilder.build()
        builder.addType(interfaceBuilt)

        def interfacePackageName = "com.talosvfx.talos.editor.notifications.actions.enums.Actions"
        def interfaceJavaFile = JavaFile.builder(interfacePackageName, interfaceBuilt).build()

        def className = ClassName.get(interfaceJavaFile.packageName, interfaceJavaFile.typeSpec.name)

        for (XmlReader.Element actionGroup : root.getChildrenByName("actionGroup")) {
            String packageName = actionGroup.getAttribute("package")
            TypeSpec.Builder enumBuilder = TypeSpec.enumBuilder(packageName).addSuperinterface(className)
            enumBuilder.addField(FieldSpec.builder(String.class, "uniqueName", Modifier.PUBLIC, Modifier.FINAL).build())
            MethodSpec uniqueName = MethodSpec.constructorBuilder().addParameter(String.class, "uniqueName").addStatement("this.uniqueName = uniqueName;").build()
            enumBuilder.addMethod(uniqueName)
            for (XmlReader.Element action : actionGroup.getChildrenByName("action")) {
                enumBuilder.addEnumConstant(action.getAttribute("name").toUpperCase(),
                        TypeSpec.anonymousClassBuilder("\$S", action.getAttribute("uniqueName")).build())
            }
            TypeSpec enumType = enumBuilder.build();
            builder.addType(enumType);
        }

        TypeSpec clazzBuild = builder.build();
        final JavaFile.Builder fileBuilder = JavaFile.builder("com.talosvfx.talos.editor.notifications.actions.enums", clazzBuild);
        JavaFile javaFile = fileBuilder.build()
        def destination = new File("$projectDir/src/")
        javaFile.writeTo(destination)
    }
}

classes {
    dependsOn createProperties
}

task packageSources(type: Jar, dependsOn: classes) {
    from delombok
    from sourceSets.main.resources.srcDirs
    archiveClassifier = 'web'
}

configurations {
    web {
        canBeConsumed true
        canBeResolved false
        extendsFrom implementation, runtimeOnly
    }
}

artifacts {
    web(packageSources)
}

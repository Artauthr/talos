allprojects {

    apply plugin: "java-library"


    ext {
        appName = "talos"
        gdxVersion = '1.10.1-SNAPSHOT'
        freetypeGwtVersion = '1.11.1-SNAPSHOT'
        lwjgl3Version = '3.3.0-SNAPSHOT'
        visuiVersion = '1.5.0'

        isReleaseBuild = {
            return project.hasProperty("RELEASE")
        }

        getReleaseRepositoryUrl = {
            return project.hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
                    : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        }

        getSnapshotRepositoryUrl = {
            return project.hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
                    : "https://s01.oss.sonatype.org/content/repositories/snapshots/"
        }
        getRepositoryUsername = {
            return project.hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
        }

        getRepositoryPassword = {
            return project.hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
        }
    }

    if (isReleaseBuild) {
        version += "-SNAPSHOT"
    }


    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }
}
